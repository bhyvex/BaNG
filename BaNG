#!/usr/bin/perl
#
# copyright 2013 Patrick Schmid <schmid@phys.ethz.ch>, distributed under
# the terms of the GNU General Public License version 2 or any later
# version.
#
# This is compiled with threading support
#
# 2013.02.20, Patrick Schmid <schmid@phys.ethz.ch> & Claude Becker <becker@phys.ethz.ch>
#
use strict;
use warnings;
use Cwd 'abs_path';
use File::Basename;
use lib dirname(abs_path($0))."/lib";
use Getopt::Long qw(:config no_auto_abbrev);
use threads;
use Thread::Queue;

use BaNG::Hosts;
use BaNG::Config;
our %hosts;

my $version         = '3.0';
my $debug           = 1;
my $debuglevel      = 2;        #1 normal output, 2 ultimate output, 3 + rsync verbose!
my $nthreads        = 1;
my $wipe            = '';
my $help            = '';
my $showversion     = '';
my $group_arg       = '';
my $host_arg        = '';
my $nthreads_arg    = '';

my @queue;
my @threads;


#################################
# Main
#
parse_command_options();
get_global_config();
get_host_config($host_arg, $group_arg);

# run wipe jobs or fill queue with backup jobs
foreach my $config (keys %hosts) {
    if ( $wipe ) {
        do_wipe(  $hosts{$config}->{hostname}, $hosts{$config}->{group});
    } else {
        queue_backup($hosts{$config}->{hostname}, $hosts{$config}->{group});
    }
}
print "Queue: @queue\n" if $debug;

# stop if queue is empty
if( !@queue ) {
    print "Exit because queue is empty.\n" if $debug;
    exit 0;
}

# use threads to empty queue
start_threads();


exit 0;


#################################
# Add task to make new backup to queue
#
sub queue_backup {
    my ($host, $group) = @_;

    print "sub queue_backup($host, $group)\n" if $debug;

    # make sure backup is enabled
    return unless $hosts{"$host-$group"}->{status} eq 'enabled';
    # stop if trying to do bulk backup if it's not allowed
    return unless ( ($group_arg && $host_arg) || $hosts{"$host-$group"}->{hostconfig}->{BKP_BULK_ALLOW});

    # make sure host is online
    my ($conn_status, $conn_msg ) = chkClientConn($host, $hosts{"$host-$group"}->{hostconfig}->{BKP_GWHOST});
    print "chkClientConn: $conn_status, $conn_msg\n" if $debug;
    if( !$conn_status ) {
        print "Error: host $host is offline\n" if $debug;
        return 1;
    }

    # check if last backup exists
    my $lastbkp = chkLastBkp($host, $group);

    # get list of partitions to back up
    my (@src_part) = split ( / /, $hosts{"$host-$group"}->{hostconfig}->{BKP_SOURCE_PARTITION});
    print "Number of partitions: " . ($#src_part+1) . " ( @src_part )\n" if $debug;

    # optionally queue each subfolder of the partitions
    if( $hosts{"$host-$group"}->{hostconfig}->{BKP_THREAD_SUBFOLDERS} ) {
        foreach my $part (@src_part) {
            queue_remote_subfolders($host,$group, $lastbkp, $part);
        }
    } else {
        # queue list of partitions as a whole
        push @queue, "${host}_${group}_${lastbkp}_@src_part";
    }

    print "Queued backup of host $host group $group\n" if $debug;

    return 1;
}

#################################
# Start threads
#
sub start_threads {

    # define number of threads
    if( $nthreads_arg ){
        # If nthreads was defined by cli argument, use it
        $nthreads = $nthreads_arg;
        print "Using nthreads = $nthreads from command line argument\n" if $debug;
    } elsif ( $host_arg && $group_arg) {
        # If no nthreads was given, and we back up a single host and group, get nthreads from its config
        $nthreads = $hosts{"$host_arg-$group_arg"}->{hostconfig}->{BKP_THREADS_DEFAULT};
        print "Using nthreads = $nthreads from $host_arg-$group_arg config file\n" if $debug;
    }

    my $Q = Thread::Queue->new;
    @threads = map threads->create( \&thread_work, $Q ), 1 .. $nthreads;
    $Q->enqueue($_) for sort @queue;
    $Q->enqueue( (undef) x $nthreads );
    $_->join for @threads;

    return 1;
}

#################################
# Work done by each thread
#
sub thread_work {
    my $Q   = shift;
    my $tid = threads->tid;
    while (my $queue_content = $Q->dequeue) {

        print "Thread $tid working on $queue_content \n" if $debug;
        do_backup($queue_content);
    }
}

#################################
# Make new backup
#
sub do_backup {
    my ($queue_content) = @_;
    my ($host, $group, $lastbkp, $path) = split( /_/, $queue_content);

    print "sub do_backup( $host, $group, $lastbkp, $path )\n" if $debug;

    my $rsync_cmd     = $globalconfig{path_rsync};
    my $rsync_options = eval_rsync_options($host,$group, $lastbkp);
    my $rsync_source  = "$host$path";
    my $rsync_target  = eval_rsync_target($host, $group);
    print "Rsync Command: $rsync_cmd $rsync_options $rsync_source $rsync_target\n" if $debug;

    print "Backup host $host group $group\n" if $debug;

    return 1;
}

#################################
# Wipe old backup
#
sub do_wipe {
    my ($host, $group) = @_;

    print "Wipe host $host group $group\n" if $debug;

    return 1;
}

#################################
# Add list of remote folders to thread queue
#
sub queue_remote_subfolders {
    my ($host, $group, $lastbkp, $partition) = @_;

    $partition =~ s/://;

    my $remoteshell   = $hosts{"$host-$group"}->{hostconfig}->{BKP_RSYNC_RSHELL};
    my @remotedirlist = `$remoteshell $host find $partition -xdev -mindepth 1 -maxdepth 1`;
    print "eval subfolders command: @remotedirlist\n" if $debug;

    foreach my $remotedir (@remotedirlist) {
        chomp $remotedir;
        push @queue, "${host}_${group}_${lastbkp}_${remotedir}";
    }

    return 1;
}
#################################
# Evaluate rsync target path
#
sub eval_rsync_target {
    my ($host, $group) = @_;

    my $rsync_target = targetpath($host, $group);

    if ($hosts{"$host-$group"}->{hostconfig}->{BKP_STORE_MODUS} eq 'snapshots' ) {
        $rsync_target .= "/current";

        if ( !-d $rsync_target ) {
            print "$globalconfig{path_btrfs} subvol create $rsync_target\n" if $debug;
        }
    } else {
        if ( !-d $rsync_target ) {
            print "mkdir -p $rsync_target\n" if $debug;
        }

        $rsync_target .= '/' . eval( $hosts{"$host-$group"}->{hostconfig}->{BKP_FOLDER} );
    }

    return $rsync_target;
}
#################################
# Evaluate rsync options
#
sub eval_rsync_options {
    my ($host, $group, $lastbkp) = @_;
    my $rsync_options  = '';
    my $hostconfig = $hosts{"$host-$group"}->{hostconfig};

    $rsync_options .= "-ax "            if $hostconfig->{BKP_RSYNC_ARCHIV};
    $rsync_options .= "-R "             if $hostconfig->{BKP_RSYNC_RELATIV};
    $rsync_options .= "-H "             if $hostconfig->{BKP_RSYNC_HLINKS};
    $rsync_options .= "--delete "       if $hostconfig->{BKP_RSYNC_DELETE};
    $rsync_options .= "--force "        if $hostconfig->{BKP_RSYNC_DELETE_FORCE};
    $rsync_options .= "--numeric-ids "  if $hostconfig->{BKP_RSYNC_NUM_IDS};
    $rsync_options .= "--inplace "      if $hostconfig->{BKP_RSYNC_INPLACE};
    $rsync_options .= "--acls "         if $hostconfig->{BKP_RSYNC_ACL};
    $rsync_options .= "--xattrs "       if $hostconfig->{BKP_RSYNC_XATTRS};
    $rsync_options .= "--no-D "         if $hostconfig->{BKP_RSYNC_OSX};
    $rsync_options .= "-v "             if ($debug && ($debuglevel == 3));
    $rsync_options .= "--rsync-path=$hostconfig->{BKP_RSYNC_RSHELL_PATH} " if $hostconfig->{BKP_RSYNC_RSHELL_PATH};

    if ($hostconfig->{BKP_EXCLUDE_FILE}) {
        my $excludefile = "$globalconfig{path_excludes}/$hostconfig->{BKP_EXCLUDE_FILE}";
        if ( -e $excludefile ) {
            $rsync_options .= "--exclude-from=$globalconfig{path_excludes}/$hostconfig->{BKP_EXCLUDE_FILE} ";
        } else {
            print "Warning: excludefile $excludefile not found.\n" if $debug;
        }
    }

    if ($hostconfig->{BKP_RSYNC_RSHELL}){
        if ($hostconfig->{BKP_GWHOST}){
            $rsync_options .= "-e $hostconfig->{BKP_RSYNC_RSHELL} $hostconfig->{BKP_GWHOST} ";
        } else {
            $rsync_options .= "-e $hostconfig->{BKP_RSYNC_RSHELL} ";
        }
    }

    # use links if told to and if lastbkp exists
    if ( ($hostconfig->{BKP_STORE_MODUS} eq 'links') && ($lastbkp ne 'nolastbkp') ) {
        $rsync_options .= "--link-dest " . targetpath($host, $group) . "/$lastbkp ";
    }

    $rsync_options =~ s/\s+$//; # remove trailing space

    return $rsync_options;
}

#################################
# Define target path of backup
#
sub targetpath {
    my ($host, $group) = @_;

    my $hostconfig  = $hosts{"$host-$group"}->{hostconfig};
    my $target_path = "$hostconfig->{BKP_TARGET_PATH}/$hostconfig->{BKP_PREFIX}/$host";

    return $target_path;
}

#################################
# Get/Check path of last backup
#
sub chkLastBkp {
    my ($host, $group) = @_;

    my $lastbkp     = "nolastbkp";
    my $hostconfig  = $hosts{"$host-$group"}->{hostconfig};
    my $bkpdir      = "$hostconfig->{BKP_TARGET_PATH}/$hostconfig->{BKP_PREFIX}/$host";
    my $lastdstfile = "$bkpdir/$hostconfig->{BKP_LASTBKP_FILE}";

    if ( -d $bkpdir  && -e $lastdstfile ){
        $lastbkp       = `cat $lastdstfile`;
        chomp($lastbkp);
    }

    print "sub chkLastBkp: lastbkp = $lastbkp\n" if $debug;

    return $lastbkp;
}

#################################
# Get/Check command options
#
sub parse_command_options {
    GetOptions (
        "help"         => sub { usage('') },
        "v|version"    => \$showversion,
        "d|debug"      => \$debug,
        "g|group=s"    => \$group_arg,
        "h|host=s"     => \$host_arg,
        "t|threads=i"  => \$nthreads_arg,
        "w|wipe"       => \$wipe,
    )
    or usage("Invalid commmand line options.");
    usage("You must provide some arguments")    unless ($host_arg || $group_arg || $showversion);
    usage("Current version number: $version")   if ( $showversion );
    usage("Number of threads must be positive") if ( $nthreads_arg && $nthreads_arg <= 0 );
}

##############
# Usage
#
sub usage {
    my ($message) = @_;

    if (defined $message && length $message) {
        $message .= "\n"
            unless $message =~ /\n$/;
    }

    my $command = $0;
    $command    =~ s#^.*/##;

    print STDERR (
        $message, qq(
        Usage Examples:

        $command -h <hostname> -g <group>   # back up given host and group
        $command -h <hostname>              # back up all groups of given host
        $command -g <group>                 # back up all hosts of given group
        $command -v                         # show version number
        $command --help                     # show this help message

        Optional Arguments:

        -t <nr>         # number of threads, default: 1
        -w              # wipe the backup
        -d              # show debugging messages
    \n)
    );
    exit 0;
}
